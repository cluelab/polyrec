plugins {
    id 'groovy'
    id 'jacoco'
    id 'project-report'
    id 'ru.vyarus.java-lib' version '1.0.5'
    id 'ru.vyarus.github-info' version '1.1.0'
    id 'ru.vyarus.animalsniffer' version '1.4.2'
    id 'ru.vyarus.quality' version '2.4.0'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'com.jfrog.bintray' version '1.8.0'
    id 'net.researchgate.release' version '2.6.0'
    id 'com.github.ben-manes.versions' version '0.17.0'
}

sourceCompatibility = 1.6

wrapper {
    gradleVersion = 3.3
}

repositories { jcenter(); mavenCentral(); mavenLocal() }
dependencies {
    signature 'org.codehaus.mojo.signature:java16:1.1@signature'

    provided 'com.google.code.findbugs:jsr305:3.0.2'
    provided 'com.google.code.findbugs:annotations:3.0.1'

    testCompile 'ch.qos.logback:logback-classic:1.2.3'
    testCompile 'org.slf4j:jul-to-slf4j:1.7.25'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
}

group = 'it.unisa.di.cluelab'
description = 'PolyRec'

github {
    user = 'cluelab'
    license = 'BSD 3-clause'
}

pom {
    developers {
        developer {
            id 'vfuccella'
            name 'Vittorio Fuccella'
            email 'vfuccella@unisa.it'
        }
    }
}

bintray {
    user = findProperty('bintrayUser')
    key = findProperty('bintrayKey')
    publications = ['maven']
    dryRun = false
    publish = true
    pkg {
        repo = 'polyrec'
        userOrg = 'cluelab'
        name = project.name
        desc = project.description
        labels = []
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
                passphrase = findProperty('gpgPassphrase')
            }
            mavenCentralSync {
                sync = false
                user = findProperty('sonatypeUser')
                password = findProperty('sonatypePassword')
            }
        }
    }
}

afterReleaseBuild {
    dependsOn = [bintrayUpload]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    testLogging {
        events 'skipped', 'failed', 'standard_error'
        exceptionFormat 'full'
    }
    maxHeapSize = '512m'
}

dependencyUpdates.revision = 'release'
jacocoTestReport.reports.xml.enabled = true


task showDependenciesTree(dependsOn: 'htmlDependencyReport', group: 'help', description:
        'Generates dependencies tree report and opens it in browser') {
    doLast {
        java.awt.Desktop.getDesktop().open(file('build/reports/project/dependencies/root.html'))
    }
}

// TODO enable pmd
quality {
    pmd = false
}
